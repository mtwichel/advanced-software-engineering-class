/* Model created by Brady Cornett and Marcus Twichel
 * Advanced Software Engineering
 * Homework 3
 * 02/24/2020
 */
model ourFPS

  enum Level {recruit, basic, master, grandmaster, godtier}
  enum WeaponType {range, melee, magic}
  enum PlayerState {neutral, panic, attack}

  abstract class Entity
    attributes
      health: Integer
      name: String
      level: Level
      xPosition: Integer
      yPosition: Integer
    operations
      attack(target:Entity)
        begin
          target.recieveDamage(self.weapon.damage, self);
        end
      move(dx: Integer, dy: Integer)
        begin
          self.xPosition := self.xPosition + dx;
          self.yPosition := self.yPosition + dy;
        end
      seePlayer(entity:Entity)
        begin
          if not self.attacker->includes(entity)
          then
            insert (self, entity) into enemiesAssociation;
            entity.seePlayer(self)
          end
        end
      loosePlayer(entity:Entity)
        begin
          delete (self, entity) from enemiesAssociation;
        end
      recieveDamage(damage: Integer, entity:Entity)
        begin
          self.health := self.health - damage;

          if self.health < 0
          then
            delete (entity, self) from enemiesAssociation;
          end
        end
    statemachines
      psm PlayerState
        states
          startup:initial
          neutral
          attack
          panic
          death [health <= 0]
          -- find final state
        transitions
          startup -> neutral { create }
          -- if our weapon is better than opponent's, we attack
          neutral -> attack { seePlayer() [self.attacker.weapon.damage->sum() < self.weapon.damage] }
          attack -> attack { recieveDamage() [health > 0] }
          panic -> panic { recieveDamage() [health > 0] }
          neutral -> panic { seePlayer() [self.attacker.weapon.damage->sum() >= self.weapon.damage] }
          attack -> neutral { loosePlayer() [self.attacker->size() = 0] }
          panic -> neutral { loosePlayer() [self.attacker->size() = 0] }
          attack -> death { recieveDamage() [health <= 0] }
          panic -> death { recieveDamage() [health <= 0] }
          attack -> neutral { attack() [self.defender->size() = 0] }
          panic -> neutral { attack() [self.defender->size() = 0] }
          attack -> attack { attack() [self.defender->size() <> 0] }
          panic -> panic { attack() [self.defender->size() <> 0] }
          
        end

  end

  class Player < Entity
    attributes
    operations
  end


  class NonPlayableCharacter < Entity
    attributes
    operations
  end

  class Weapon
    attributes
      name: String
      damage: Integer
      type: WeaponType
  end

-- associations
association enemiesAssociation between
  Entity[0..*] role attacker
  Entity[0..*] role defender
end

association entityWeapon between
  Entity[1] role entity
  Weapon[1] role weapon
end